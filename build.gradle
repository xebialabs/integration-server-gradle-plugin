buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }

    dependencies {
        if (project.gradle.startParameter.taskNames.contains("final")) {
            classpath "com.netflix.nebula:nebula-release-plugin:${nebulaReleasePluginVersion}"
        }
    }
}

plugins {
    id 'groovy'
    id 'idea'
    id 'maven'
    id "org.ajoberstar.release-opinion" version "1.4.2"
}

group = 'com.xebialabs.gradle.plugins'
defaultTasks 'build'

sourceCompatibility = languageLevel
targetCompatibility = languageLevel

repositories {
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
    maven {
        credentials {
            username project.property("nexusUserName")
            password project.property("nexusPassword")
        }
        url "${project.property('nexusBaseUrl')}/repositories/releases"
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

release {
    grgit = org.ajoberstar.grgit.Grgit.open(dir: project.projectDir)
    defaultVersionStrategy = org.ajoberstar.gradle.git.release.opinion.Strategies.SNAPSHOT
    tagStrategy {
        toTagString = { vs -> "${project.name}-$vs" }
        parseTag = { org.ajoberstar.grgit.Tag tag ->
            if (tag.name.startsWith(project.name)) {
                com.github.zafarkhaja.semver.Version.valueOf(tag.name[project.name.size() + 1..-1])
            } else {
                null
            }
        }
    }
}

configurations.compile.transitive = false

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation("org.codehaus.groovy.modules.http-builder:http-builder:$httpBuilderVersion") {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    implementation "de.vandermeer:asciitable:$asciitableVersion"
    implementation "commons-io:commons-io:$commonsIOVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "org.postgresql:postgresql:${project.properties['driverVersions.postgres']}"
    implementation "mysql:mysql-connector-java:${project.properties['driverVersions.mysql']}"
    implementation "org.dbunit:dbunit:$dbUnitVersion"
    implementation "gradle.plugin.com.palantir.gradle.docker:gradle-docker:$dockerPluginVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

test {
    useJUnitPlatform()
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "${project.property("nexusBaseUrl")}/repositories/releases") {
            authentication(userName: "${project.property("nexusUserName")}", password: "${project.property("nexusPassword")}")
        }
        snapshotRepository(url: "${project.property("nexusBaseUrl")}/repositories/snapshots") {
            authentication(userName: "${project.property("nexusUserName")}", password: "${project.property("nexusPassword")}")
        }
    }
}

tasks.release.dependsOn(build, uploadArchives)
