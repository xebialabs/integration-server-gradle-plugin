plugins {
    id "com.github.node-gradle.node" version "3.1.0"
    id 'groovy'
    id 'idea'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'maven-publish'
    id 'nebula.release' version '15.3.1'
    id 'signing'
}

group = 'com.xebialabs.gradle.plugins'
defaultTasks 'build'

def releasedVersion = "10.3.0-${(new Date().format('Mdd.Hmm'))}"
project.ext.set("releasedVersion", releasedVersion)

sourceCompatibility = languageLevel
targetCompatibility = languageLevel

repositories {
    mavenLocal()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url 'https://oss.sonatype.org/service/local/repositories/releases/content'
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"
    implementation "com.xebialabs.gradle.plugins:gradle-xl-derby-plugin:$gradleXlDerbyPluginVersion"
    implementation "commons-io:commons-io:$commonsIOVersion"
    implementation "de.vandermeer:asciitable:$asciitableVersion"
    implementation "com.palantir.gradle.docker:gradle-docker:$dockerPluginVersion"
    implementation "mysql:mysql-connector-java:${project.properties['driverVersions.mysql']}"
    implementation("org.codehaus.groovy.modules.http-builder:http-builder:$httpBuilderVersion") {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    implementation "org.dbunit:dbunit:$dbUnitVersion"
    implementation "org.postgresql:postgresql:${project.properties['driverVersions.postgres']}"


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

test {
    useJUnitPlatform()
}

if (project.hasProperty("sonatypeUsername") && project.hasProperty("public")) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)

                groupId 'com.xebialabs.gradle.plugins'
                artifactId 'integration-server-gradle-plugin'
                version releasedVersion

                pom {
                    name = 'Integration Server Gradle Plugin'
                    description = 'The easy way to get custom setup for Deploy up and running'
                    url = 'https://github.com/xebialabs/integration-server-gradle-plugin.git'
                    licenses {
                        license {
                            name = 'GPLv2 with Digital.ai FLOSS License Exception'
                            url = 'https://github.com/xebialabs/integration-server-gradle-plugin/blob/master/LICENSE'
                        }
                    }

                    scm {
                        url = 'https://github.com/xebialabs/integration-server-gradle-plugin'
                    }

                    developers {
                        developer {
                            id = 'bnechyporenko'
                            name = 'Bogdan Nechyporenko'
                            email = 'bnechyporenko@digital.ai'
                        }
                        developer {
                            id = 'aalbul'
                            name = 'Alexander Albul'
                            email = 'aalbul@digital.ai'
                        }
                        developer {
                            id = 'sishwarya'
                            name = 'Ishwarya Surendrababu'
                            email = 'sishwarya@digital.ai'
                        }
                        developer {
                            id = 'tonac'
                            name = 'Antonio Sostar'
                            email = 'asostar@digital.ai'
                        }
                    }
                }
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username = project.property("sonatypeUsername")
                    password = project.property("sonatypePassword")
                }
            }
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots"
                credentials {
                    username = project.property("sonatypeUsername")
                    password = project.property("sonatypePassword")
                }
            }
        }
    }
} else {
    publishing {
        publications {
            myLibrary(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url "${project.property("nexusBaseUrl")}/repositories/releases"
                credentials {
                    username = project.property("nexusUserName")
                    password = project.property("nexusPassword")
                }
            }
        }
    }
}

jar {
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

if (project.hasProperty("sonatypeUsername") && project.hasProperty("public")) {
    signing {
        sign publishing.publications.mavenJava
    }

    nexusPublishing {
        repositories {
            sonatype {
                username = project.property("sonatypeUsername")
                password = project.property("sonatypePassword")
            }
        }
    }
}

import com.xebialabs.gradle.release.NebulaRelease

task nebulaRelease(type: NebulaRelease)

task uploadArchives {
    dependsOn publish
}

task dumpVersion {
    doLast {
        file(buildDir).mkdirs()
        def out = file("$buildDir/version.dump")
        out.write("version=$project.version")
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

yarn_install {
    args = ['--mutex', 'network']
}

node {
    version = '14.17.5'
    yarnVersion = '1.22.11'
    download = true
}

task docRun(type: YarnTask, dependsOn: [yarn_install]) {
    args = ["run", "start"]
    workingDir = file("${rootDir}/docs")
}
